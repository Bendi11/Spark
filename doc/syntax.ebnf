
<path> ::= <ident> ( ":" <ident> )*
<importdecl> ::= "imp" <path>

<fundef> ::= <fundecl> <body>
<fundecl> ::= "fun" <ident> ( <typename> <ident> "," )* ( <typename> <ident> )? ( "->" <typename> )? "extern"?

<typedef> ::= <typedecl> <structfields>
            | <typedecl> <typename>
            | <typedecl> <typename> ( "|" <typename> )+
<structfields> ::= "{" ( <typename> <ident> "," )* ( <typename> <ident>? ) "}"

<typedecl> ::= "type" <user-typename> "="

<stmt> ::= <callexpr> | <assignstmt> | <varstmt> | <retstmt> | <matchexpr> | <ifexpr> | "break" | "cont"

<matchcase> ::= <user-typename> <ident>?

<matcharm> ::= <matchcase>? "->" ( <body> | <expr> | <phistmt> )
<matchexpr> ::= "match" <expr> "{" ( <matcharm> "," )* <matcharm>? "}"

<retstmt> ::= "return" <expr>?
<phistmt> ::= "phi" <expr>
<assignstmt> ::= ( <varaccessexpr> | <varstmt> ) "=" <expr>
<varstmt> ::= ( "let" | "mut" ) ( "(" <typename> ")" )? <ident>
<loopstmt> ::= "loop" <body>
<ifexpr> ::= "if" <expr> <body> ( ( "else" <body> ) | ( "else" <ifstmt> ) )

<body> ::= "{" <stmt>* "}"

<expr> ::= <literal> | <prefix> | <op> <expr> | <expr> <op> <expr> | <ifexpr> | <matchexpr>
        | "$" <typename> <expr>

<exprlist> ::= ( <expr> "," )* <expr>?

<prefixexpr> ::= <varaccessexpr> | <callexpr> | "(" <expr> ")"

<callexpr> ::= <path> "(" <exprlist> ")"
<varaccessexpr> ::= <path>
                  | <prefix> "[" <expr> "]"
                  | <prefix> "." <ident>

<typename> ::= "bool" | "char" | "()"
             | ( "i" | "u" ) ( "8" | "16" | "32" | "64" )
             | "f" ( "32" | "64" )
             | <user-typename>
             | "*" <typename>
             | <array-typename>

<array-typename> ::= "[" <digit>+? "]" <typename>

<user-typename> ::= <path> ( "|" ( (<typename> "," )* <typename>? ) "|" )?

<literal> ::= <string-literal> | <bool-literal> | <char-literal> | <number-literal>

<string-literal> ::= "\"" ( . | ( "\\" . ) )* "\""
<bool-literal> ::= "true" | "false"
<char-literal> ::= "'" ( . | "\\" . ) "'"
<array-literal> := "[" (<exprlist> "]"
<tuple-literal> ::+ "(" <exprlist> ")"
<struct-literal> ::= <user-typename> "{" ( <ident> "=" <expr> "," )* ( <ident> "=" <expr> )? "}"

<op> ::= "+" | "-" | "*" | "/" | "%" | "&" | "|" | "~" | "^" | "<" | ">" | "=="
       | "<=" | ">=" | "<<" | ">>" | "&&" | "||" | "!" | "!="

<number-literal> ::= <digit>+ "."? <digit>* ( "e" ( "+" | "-" ) <digit>+)?
                   | "0b" ( "0" | "1" )+
                   | "0x" ( <digit> | "a" | "b" | "c" | "d" | "e" | "f" )+
                   | "0o" ( "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" )+

<ident> ::= .+
<digit> ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
